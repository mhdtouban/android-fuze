apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: "me.tatarka.retrolambda"

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            all {
                jvmArgs "-noverify"
            }
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "io.gmas.fuze"
        minSdkVersion 19
        targetSdkVersion 25
        multiDexEnabled  true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }

    flavorDimensions "AUDIENCE", "API"

    productFlavors {
        pre21 {
            dimension "API"
        }

        min21 {
            dimension "API"
            minSdkVersion 21
        }
        external {
            dimension "AUDIENCE"
            applicationId "com.octopepper.yummypets"
            versionCode 1
            versionName "1.0"
        }
    }

    lintOptions {
        quiet true
        warningsAsErrors true
        lintConfig file(project.rootDir.path + "/app/lint.xml")
    }

    applicationVariants.all { variant ->
        def apkName = "Fuze";
        if (variant.buildType.zipAlignEnabled) {
            apkName += "-zipaligned";
        } else {
            apkName += "-unaligned";
        }
        if (variant.buildType.name == "release") {
            apkName += ".apk";
        } else {
            apkName += "-SNAPSHOT.apk";
        }
        variant.outputs.each { output ->
            output.outputFile = file("$project.buildDir/outputs/apk/" + apkName);
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode true
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/LICENSE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/rxjava.properties"
        pickFirst "AndroidManifest.xml"
    }

    sourceSets {
        main { java.srcDirs = ["src/main/java",
                               "src/internal/java",
                               "src/external/java"] }
    }

}

retrolambda {
    jvmArgs "-noverify"
}

ext.robolectric_version = "3.3.2"
ext.android_support_version = "25.3.1"
ext.play_services_version = "11.0.1"
ext.dagger_version = "2.10"
ext.butterknife_version = "8.5.1"
ext.retrofit_version = "2.2.0"
ext.leakcanary_version = "1.5"
ext.fresco_version = "1.3.0"
ext.rxlifecycle_version = "2.0.1"
ext.rxbinding_version = "2.0.0"

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])

    testCompile "org.mockito:mockito-core:2.7.22"
    testCompile "org.robolectric:robolectric:$robolectric_version"
    testCompile "org.robolectric:shadows-multidex:$robolectric_version"
    testCompile "org.robolectric:shadows-play-services:$robolectric_version"
    testCompile "org.robolectric:shadows-support-v4:$robolectric_version"
    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.google.code.findbugs"
    })
    androidTestCompile "com.android.support:support-annotations:25.3.1"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile "com.android.support.test:runner:0.5"
    testCompile "junit:junit:4.12"

    compile "com.android.support:design:$android_support_version"
    compile "com.android.support:customtabs:$android_support_version"
    compile "com.android.support:cardview-v7:$android_support_version"
    compile "com.android.support:recyclerview-v7:$android_support_version"
    compile "com.android.support:support-v4:$android_support_version"
    compile "com.android.support:support-v13:$android_support_version"
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:palette-v7:$android_support_version"
    compile "com.android.support:support-core-utils:$android_support_version"
    compile "com.android.support:multidex:1.0.1"
    compile "com.google.android.gms:play-services-gcm:$play_services_version"
    compile "com.google.android.gms:play-services-analytics:$play_services_version"
    compile "com.google.android.gms:play-services-ads:$play_services_version"
    compile "com.google.auto.value:auto-value:1.4"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:0.2.5"
    compile "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    compile "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"
    compile "com.jakewharton.timber:timber:4.5.1"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.okhttp3:logging-interceptor:3.6.0"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    compile "com.facebook.fresco:fresco:$fresco_version"
    compile "com.facebook.fresco:webpsupport:$fresco_version"

    compile "com.rengwuxian.materialedittext:library:2.1.4"
    compile "com.pnikosis:materialish-progress:1.7"
    compile "net.danlew:android.joda:2.9.9"

    compile "io.reactivex.rxjava2:rxjava:2.0.8"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
    compile "com.trello.rxlifecycle2:rxlifecycle:$rxlifecycle_version"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:$rxlifecycle_version"
    compile "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxbinding_version"
    compile "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxbinding_version"
    compile "com.jakewharton.rxbinding2:rxbinding-design:$rxbinding_version"
    compile "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxbinding_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}